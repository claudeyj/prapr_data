assert project == rootProject

File versionFile = project.rootProject.file("version.properties")
project.ext.loadVersion = {
    Properties props = new Properties()
    props.load(new FileInputStream(versionFile))
    props.version
}

project.version = project.ext.loadVersion()

logger.lifecycle "Version: $project.version"

task incrementVersion {
    description "Increments version in 'version.properties' file."
    doLast {
        try {
            List numbers = project.version.split('\\.')
            //TODO remove -dev hack once we're happy with releases.
            //TODO version file will have -SNAPSHOT name but Travis with remove -SNAPSHOT during the build
            int micro = (numbers.pop() - "-dev") as Integer
            numbers << micro + 1
            def newVersion = numbers.join('.') + "-dev"
            def updatedContent = versionFile.text.replaceAll("(?s)version=(.*?)\n", "version=$newVersion\n")
            versionFile.text = updatedContent
            logger.lifecycle("Current content of '{}':\n----------\n{}\n----------", versionFile.name, updatedContent)
        } catch (Exception e) {
            throw new GradleException("Unable to increment minor version.", e)
        }
    }
}