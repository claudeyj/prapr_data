apply plugin: 'cpp'

model {
    platforms {
        x86 {
            architecture "x86"
        }
        x64 {
            architecture "x86_64"
        }
        itanium {
            architecture "ia-64"
        }
    }
}

model {
    buildTypes {
        debug
        release
    }
}

executables {
    main {
        targetPlatforms "x86", "x64"
    }
}
libraries {
    hello {
        targetPlatforms "x86", "x64"
    }
}
sources {
    main.cpp.lib libraries.hello.static
}

// Apply arguments for debug binaries (these options are not yet set automatically)
binaries.all {
    if (toolChain in Gcc && buildType == buildTypes.debug) {
        cppCompiler.args "-g"
    }
    if (toolChain in VisualCpp && buildType == buildTypes.debug) {
        cppCompiler.args '/Zi'
        cppCompiler.define 'DEBUG'
        linker.args '/DEBUG'
    }
}

// Apply custom arguments for different target platforms
binaries.all {
    if (toolChain in Gcc && targetPlatform == platforms.x86) {
        cppCompiler.args '-O3'
    }
}

// Tasks to build all binaries for a tool chain
task gccExecutables {
    dependsOn binaries.withType(NativeExecutableBinary).matching {
        it.toolChain in Gcc
    }
}
task visualCppExecutables {
    dependsOn binaries.withType(NativeExecutableBinary).matching {
        it.toolChain in VisualCpp
    }
}
